plugins {
	id 'com.google.protobuf' version '0.8.8'
	id 'java'
	id 'me.champeau.gradle.jmh' version '0.4.8'
}

// to make build/generated/source/proto/main/java treated as sources root
apply plugin: 'idea'
// to make build/generated/source/proto/main/java compiled
apply plugin: 'com.google.protobuf'

group = 'demo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	// to resolve "error: package com.google.protobuf does not exist"
	implementation 'com.google.protobuf:protobuf-java:3.7.1'
	// to support json formatting
	implementation 'com.google.protobuf:protobuf-java-util:3.7.1'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
	implementation 'com.hubspot.jackson:jackson-datatype-protobuf:0.9.10-jackson2.9-proto3'
	implementation 'io.github.benas:random-beans:3.9.0'
	implementation 'org.mapstruct:mapstruct:1.3.0.Final'
	implementation 'commons-io:commons-io:2.6'
	implementation 'org.apache.commons:commons-lang3:3.9'

	// to resolve "java.lang.ClassNotFoundException: Cannot find implementation for"
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.0.Final'
	
	testImplementation 'junit:junit:4.12'
	testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.3.0.Final'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.7.1'
	}
}